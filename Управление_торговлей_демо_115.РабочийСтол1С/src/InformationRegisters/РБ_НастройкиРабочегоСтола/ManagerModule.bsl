
// Записать настройки рабочего стола.
// 
// Параметры:
//  Пользователи - Массив Из СправочникСсылка.Пользователи,СправочникСсылка.Пользователи - Массив пользователей
//  Настройка - СправочникСсылка.РБ_ВариантыНастроекРабочегоСтола - Настройка
Процедура ЗаписатьНастройкиРабочегоСтола(Пользователи, Настройка) Экспорт
	
	Если ТипЗнч(Пользователи) <> Тип("Массив") Тогда
		СписокПользователей = Новый Массив();
		СписокПользователей.Добавить(Пользователи);
	Иначе
		СписокПользователей = Пользователи;
	КонецЕсли;
	
	Для Каждого Пользователь Из СписокПользователей Цикл
		МенеджерЗаписи = РегистрыСведений.РБ_НастройкиРабочегоСтола.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Настройка = Настройка;
		МенеджерЗаписи.Записать();
	КонецЦикла;
КонецПроцедуры

// Удалить записи регистра по настройке.
// 
// Параметры:
//  Настройка - СправочникСсылка.РБ_ВариантыНастроекРабочегоСтола - Настройка
Процедура УдалитьЗаписиРегистраПоНастройке(Настройка) Экспорт
	ТЗПользователей = ПолучитьСписокПользователейВариантаНастройки(Настройка);
	Для Каждого СтрПользователь Из ТЗПользователей Цикл
		УдалитьЗапись(СтрПользователь.Пользователь);
	КонецЦикла;	
КонецПроцедуры

Процедура УдалитьЗапись(Пользователь)
	НаборЗаписей  = РегистрыСведений.РБ_НастройкиРабочегоСтола.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
КонецПроцедуры

// Получить настройку рабочего стола.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь
// 
// Возвращаемое значение:
//  СправочникСсылка.РБ_ВариантыНастроекРабочегоСтола - Получить настройку рабочего стола
Функция ПолучитьНастройкуРабочегоСтола(Пользователь) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	РБ_НастройкиРабочегоСтола.Настройка
						  |ИЗ
						  |	РегистрСведений.РБ_НастройкиРабочегоСтола КАК РБ_НастройкиРабочегоСтола
						  |ГДЕ
						  |	РБ_НастройкиРабочегоСтола.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Настройка;
	КонецЕсли;
	Возврат Справочники.РБ_ВариантыНастроекРабочегоСтола.ПустаяСсылка();
КонецФункции


// Получить список пользователей варианта настройки.
// 
// Параметры:
//  ВариантНастройки - СправочникСсылка.РБ_ВариантыНастроекРабочегоСтола - Вариант настройки
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Получить список пользователей варианта настройки
Функция ПолучитьСписокПользователейВариантаНастройки(ВариантНастройки) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	РБ_НастройкиРабочегоСтола.Пользователь
	|ИЗ
	|	РегистрСведений.РБ_НастройкиРабочегоСтола КАК РБ_НастройкиРабочегоСтола
	|ГДЕ
	|	РБ_НастройкиРабочегоСтола.Настройка = &ВариантНастройки");
	Запрос.УстановитьПараметр("ВариантНастройки", ВариантНастройки);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции